plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(17)
}

group = 'com.github.eventmanager'
version = '2.1.5'


java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/thebloodyamateur/EventManager")
            credentials {
                username = System.getenv("GPR_USERNAME") ?: project.findProperty("gpr.username")
                password = System.getenv("GPR_TOKEN") ?: project.findProperty("gpr.token")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = 'com.github.TheBloodyAmateur'
            artifactId = 'EventManager'
            version = '2.1.5'
        }
    }
}

tasks.javadoc {
    failOnError = false // Avoid build failures due to warnings
    options.encoding = "UTF-8"
}

signing {
    if (!System.getenv("JITPACK")) {  // Disable signing on JitPack
        sign publishing.publications.maven
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}