plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.eventmanager'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_23
    targetCompatibility = JavaVersion.VERSION_23
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'EventManager'
                description = 'A simple event management library for Groovy applications.'
                url = 'https://github.com/TheBloodyAmateur/EventManager'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'thebloodyamateur'
                        name = 'Botan Celik'
                        email = 'botan.celik@icloud.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/TheBloodyAmateur/EventManager.git'
                    developerConnection = 'scm:git:ssh://github.com/TheBloodyAmateur/EventManager.git'
                    url = 'https://github.com/TheBloodyAmateur/EventManager'
                }
            }
        }
    }
    repositories {
        maven {
            name = "EventManager"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            findProperty("signing.keyId"),
            System.getenv("SIGNING_KEY"),  // Load key from memory
            findProperty("signing.password")
    )
    sign publishing.publications.mavenJava
}

dependencies {
    // COMPILE TIME
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    // RUNTIME
    implementation 'org.json:json:20231013'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'

    // TESTING
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}