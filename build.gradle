plugins {
    id 'java-library'
    id 'maven-publish'
}

java {
    withJavadocJar()
    withSourcesJar()
}

group = 'io.github.thebloodyamateur'
version = '1.0.2'

java {
    sourceCompatibility = JavaVersion.VERSION_23
    targetCompatibility = JavaVersion.VERSION_23
}

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/thebloodyamateur/EventManager")
            credentials {
                username = System.getenv("GPR_USERNAME") ?: project.findProperty("gpr.username")
                password = System.getenv("GPR_TOKEN") ?: project.findProperty("gpr.token")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'EventManager'
                description = 'A simple logging and event management library for Java.'
                url = 'https://github.com/thebloodyamateur/EventManager'

                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://mit-license.org'
                    }
                }

                developers {
                    developer {
                        id = 'thebloodyamateur'
                        name = 'Botan Celik'
                        email = 'botan.celik@icloud.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/thebloodyamateur/EventManager.git'
                    developerConnection = 'scm:git:ssh://github.com/thebloodyamateur/EventManager.git'
                    url = 'https://github.com/thebloodyamateur/EventManager'
                }
            }
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation 'org.json:json:20231013'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}